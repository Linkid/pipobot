# Maintainer: SÃ©bastien Leduc <sebastien@sleduc.fr>
pkgname=pipobot-git
_projectname=pipobot
pkgver=298.572da78
pkgrel=1
pkgdesc="A modular bot for jabber MUCs"
arch=("any")
url="https://github.com/pipoteam/pipobot"
license=('GPL')
depends=('python2' 'python2-sleekxmpp' 'python2-yaml' 'python2-sqlalchemy' )
conflicts=('pipobot')
optdepends=("pipobot-modules-git: a set of modules to add functionalities to pipobot"
            "twisted: used for the testing mode of pipobot (to test modules without an XMPP server)")
makedepends=('python2' 'git')
backup=('etc/pipobot.conf.yml'
        'etc/conf.d/pipobot')
install="$pkgname.install"
source=("${_projectname}::git+https://github.com/pipoteam/pipobot")
md5sums=('SKIP')

pkgver () {
  cd $_projectname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}
build() {
  cd "$srcdir/$_projectname"
  python2 setup.py build
}

package() {
  cd "$srcdir/$_projectname"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  install -d -m750 "$pkgdir"/var/lib/pipobot
  install -d -m750 "$pkgdir"/var/log/pipobot
  install -D -m660 pipobot.conf.yml "$pkgdir"/etc

  cd "$srcdir/$_projectname/distrib/systemd"
  install -D -m644 pipobot.service "$pkgdir"/usr/lib/systemd/system/pipobot.service
  install -D -m644 pipobot.tmpfiles.d "$pkgdir"/usr/lib/tmpfiles.d/pipobot.conf
  install -D -m644 pipobot.conf "$pkgdir"/etc/conf.d/pipobot
}

# vim:set ts=2 sw=2 et:
